buildscript {
    ext.kotlin_version = '1.8.10'
    if (!project.hasProperty('version') || project.version.equals('unspecified')) {
        project.version = '+'
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://apptentive.jfrog.io/artifactory/NewAndroidSDK"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.1'
        classpath 'com.mparticle:android-kit-plugin:' + project.version
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.jlleitschuh.gradle.ktlint" version "11.1.0"
    id 'org.gradle.maven-publish'
}

sonarqube {
    properties {
        property "sonar.projectKey", "mparticle-android-integration-apptentive"
        property "sonar.organization", "mparticle"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.mparticle.kit'
apply plugin: 'kotlin-android'

android {
    namespace 'com.mparticle.kits.apptentive'
    defaultConfig {
        minSdkVersion 16
        resValue "string", "apptentive_distribution", "mParticle"
        resValue "string", "apptentive_distribution_version", project.version
    }
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.mparticle'
            artifactId = 'android-apptentive-kit'
            version = "6.0.4"
            artifact("$buildDir/outputs/aar/android-apptentive-kit-release.aar")
            artifact androidSourcesJar
        }
    }
    repositories {
        mavenLocal()
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://apptentive.jfrog.io/artifactory/NewAndroidSDK"
        }
    }
}

dependencies {
    implementation 'com.apptentive:apptentive-kit-android:6.0.3-public'
    testImplementation 'io.mockk:mockk:1.13.3'
}
